buildscript {
    ext.kotlin_version = "1.3.72"
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
            exclude group: 'net.sf.trove4j', module: 'trove4j'
            exclude group: 'trove', module: 'trove'
        }
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

plugins {
    id "com.anatawa12.tools.decompileCrasher" version "1.2.3"
    id "com.dorongold.task-tree" version "1.3.1"
}

apply plugin: 'forge'
apply plugin: 'kotlin'
apply plugin: 'com.anatawa12.tools.decompileCrasher'
apply plugin: 'idea'

version = "2.1"
group= "Industry.Electrical.kuma.RedstoneChunkLoader-[1.7.10]" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "RedstoneChunkLoader-[1.7.10]"

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "eclipse"
    srgExtra "PK: kotlin kotlin"
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    shade "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    shade "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    def tasks = ["runClient", "build"]
    if (!project.gradle.startParameter.taskNames.stream().anyMatch { tasks.contains(it) }) {
        compileOnly files("$projectDir/${minecraft.runDir}/mods/Optifine-generated.jar")
    }
    testImplementation 'junit:junit:4.12'
    testImplementation "io.mockk:mockk:1.9.3"
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude '**/*.kotlin_metadata'
            exclude 'META-INF', 'META-INF/**'
        }
    }
}
compileJava {
    options.sourcepath = null
}
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = [
                "-XXLanguage:+InlineClasses",
                "-Xuse-experimental=kotlin.Experimental",
                "-XXLanguage:+TrailingCommas",
        ]
    }
}
repositories {
    mavenCentral()
}
